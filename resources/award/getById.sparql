PREFIX org: <http://www.w3.org/ns/org#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ocds: <http://data.tbfy.eu/ontology/ocds#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
SELECT ( ?id AS ?oid ) ?qtitle ?qdescription ?qdate ?qstatus ?qstartdate ?qenddate ?qamount ?qcurrency ?qtenderid ?qtendertitle ?qtenderdescription ?qtenderstatus
WHERE {

#Award information
  ?id a ocds:Award .
  OPTIONAL { ?id dct:title ?qtitle } .
  OPTIONAL { ?id dct:description ?qdescription } .
  OPTIONAL { ?id ocds:awardDate ?qdate } .
  OPTIONAL { ?id ocds:awardStatus ?qstatus } .

#Object Property hasAwardedContractPeriod
  OPTIONAL { ?id ocds:hasAwardedContractPeriod ?cperiod .
  OPTIONAL { ?cperiod ocds:periodStartDate ?qstartdate .
             ?cperiod ocds:periodEndDate ?qenddate }} .

#Object Property hasAwardValue
  OPTIONAL { ?id ocds:hasAwardValue ?valr .
  OPTIONAL { ?valr ocds:valueAmount ?qamount .
             ?valr ocds:valueCurrency ?qcurrency }} .

#Object Property hasDocument not implemented here, second level resource

#Object Property isIssuedForTender
  ?id ocds:isIssuedForTender ?qtenderid .
  OPTIONAL { ?qtenderid dct:title ?qtendertitle } .
  OPTIONAL { ?qtenderid dct:description ?qtenderdescription } .
  OPTIONAL { ?qtenderid ocds:tenderStatus ?qtenderstatus } .

}
