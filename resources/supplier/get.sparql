PREFIX rov: <http://www.w3.org/ns/regorg#>
PREFIX dbp: <http://dbpedia.org/ontology/>
PREFIX ocds: <http://data.tbfy.eu/ontology/ocds#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ebg: <http://data.businessgraph.io/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT ?ss ?supaward ?qlegalname ?qjurisdiction ?qnumemployees ?qawarddate ?qawardtitle
?qawarddescription ?qawardstatus ?qawardstartdate ?qawardenddate ?qawardamount ?qawardcurrency
WHERE {
  ?s ocds:isSupplierFor ?supaward .
  ?s owl:sameAs ?ss .
  ?ss rov:legalName ?qlegalname .
  ?ss dbp:jurisdiction ?qjurisdiction .
  ?ss ebg:numberOfEmployees ?qnumemployees .

  OPTIONAL { ?supaward dct:title ?qawardtitle } .
  OPTIONAL { ?supaward dct:description ?qawarddescription } .
  OPTIONAL { ?supaward ocds:awardDate ?qawarddate } .
  OPTIONAL { ?supaward ocds:awardStatus ?qawardstatus } .

  #Object Property hasAwardedContractPeriod
  OPTIONAL { ?supaward ocds:hasAwardedContractPeriod ?cperiod .
  OPTIONAL { ?cperiod ocds:periodStartDate ?qawardstartdate .
             ?cperiod ocds:periodEndDate ?qawardenddate }} .

  #Object Property hasAwardValue
  OPTIONAL { ?supaward ocds:hasAwardValue ?valr .
  OPTIONAL { ?valr ocds:valueAmount ?qawardamount .
             ?valr ocds:valueCurrency ?qawardcurrency }} .

  FILTER ( ?name = xsd:string("_empty_") || regex( ?qlegalname, ?name, "i") ) .
  FILTER ( ?jurisdiction = xsd:string("_empty_") || regex( ?qjurisdiction, ?jurisdiction, "i" ) ) .
  FILTER ( ?minemployees = xsd:string("_empty_") || ?minemployees < ?qnumemployees )
  FILTER ( ?status = xsd:string("_empty_") || regex( ?qawardstatus, ?status, "i") ) .
  FILTER ( ?title = xsd:string("_empty_") || regex( ?qawardtitle, ?title, "i") ) .
  FILTER ( ?description = xsd:string("_empty_") || regex( ?qawarddescription, ?description, "i") ) .
  FILTER ( ?currency = xsd:string("_empty_") || regex( ?qawardcurrency, ?currency, "i") ) .
  FILTER ( ?start_dt = xsd:string("_empty_") || ?qawarddate > ?start_dt ) .
  FILTER ( ?end_dt = xsd:string("_empty_") || ?qawarddate < ?end_dt ) .
}
